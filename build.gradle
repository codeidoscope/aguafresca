plugins {
    id 'application'
    id 'java'
    id 'jacoco'
}

group 'com.github.codeidoscope'
version '1.0-SNAPSHOT'
jar.archiveName = 'server.jar'

sourceCompatibility = JavaVersion.VERSION_1_10
targetCompatibility = JavaVersion.VERSION_1_10

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
    compile 'com.sun.activation:javax.activation:1.2.0'
}

application {
    mainClassName = 'com.github.codeidoscope.Server'
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'com.github.codeidoscope.Server'
        )
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    destinationDir = file("$buildDir")
}

wrapper {
    gradleVersion = '4.10.2'
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        html.enabled true
    }
}

jacocoTestCoverageVerification {
    violationRules.rule {
        limit { counter = 'INSTRUCTION'; minimum = 0.50 }
    }
}

check.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification